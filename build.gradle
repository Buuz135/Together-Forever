buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'com.selesse:gradle-git-changelog:0.2.+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.10"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.matthewprenger.cursegradle" version "1.0.10"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'com.selesse.git.changelog'
apply plugin: 'java'

String b = ""
if (project.hasProperty("teamcity")) {
    b = System.getenv("BUILD_NUMBER")
}

if (b == null || b == "") {
    version = "${project.minecraftVersion}-${modVersion}"
} else {
    version = "${project.minecraftVersion}-${modVersion}-" + b
}

group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.mcmoddev.com'
    }
    maven { // CraftTweaker
        name 'jared maven'
        url "http://maven.blamejared.com/"
    }
}
dependencies {
    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:${version_bookshelf}"
    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:${version_gamestages}"
    deobfCompile "codersafterdark.reskillable:Reskillable:1.12.2-1.9.1-SNAPSHOT.93"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

changelog {
    title = "Together Forever"
    fileName = "CHANGELOG.md"
    outputDirectory = file("$projectDir")
    since = '1.0.10'
    commitFormat = '%s (%an)'
    markdown {
        commitFormat = '* %s (%an)'
    }
    includeLines = {
        !it.contains("Merge")
    }
    processLines = {
        String input = it as String
        if (input.contains('[ci skip] ')) {
            input = input.minus('[ci skip] ')
        }
        input
    }
}

curseforge {
    if (project.hasProperty('curse_api')) {
        apiKey = project.curse_api
    }
    project {
        id = '285968'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = 'beta'

        mainArtifact(jar){
            relations {
                optionalLibrary 'game-stages'
                optionalLibrary 'reskillable'
            }
        }

        addArtifact sourceJar
    }
}

license {
    header rootProject.file('HEADER')
    include "**/*.java"
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Buuz135'
    ignoreFailures = true
    mapping {
        java='SLASHSTAR_STYLE'
    }
}
